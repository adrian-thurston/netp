void generateMainHeader( P: program )
{
	Output = new parser<out_c::out_c>
	Thread: thread = ThreadMap->find( 'Main' )

	send Output
		~/*
		~ * main_gen.h
		~ */
		~
		~#ifndef _MAIN_GEN_H
		~#define _MAIN_GEN_H
		~
		~#include "thread.h"
		~#include "itq_gen.h"
		~

	threadForwards( Thread )

	send Output
		~struct MainGen
		~	: public Thread
		~{
		~	MainGen() : Thread("main") {}
		~
		~	void init( int argc, char *const*argv )
		~	{
		~		this->argc = argc;
		~		this->argv = argv;
		~	}
		~
		~	int argc;
		~	char *const*argv;
		~
		~	int mainSignalLoop();
		~	void parseOptions();
		~	void maybeBackground();
		~	int start();
		~	void join();
		~	virtual int main() { return 0; }
		~
		~	void sigmaskSetup();
		~	int mainSignal();

	createChildDecls( Thread )
	sendsToDecls( Thread )

	send Output
		~};
		~
		~#endif

	send Output [] eos
	writeOutput( Output->tree 'main_gen.h' )
}

void optString( P: program )
{
	send Output
		"const char optstring\[\] = \":"

	for OD: option_def in P {
		for OS: option_spec in OD {
			require OS [`- short]

			send Output [OS.short]
			if !match OD.option_type [`bool]
				send Output ":"
		}
	}

	send Output
		"\";
		"
}

void longOpts( P: ref<program> )
{
	send Output
		"const option longopts\[\] = {

	Ind: int = 0
	for OD: option_def in P {
		for OS: option_spec in OD {
			require OS [`-- long]
			send Output
				"	{ \"[OS.long]\", "

			if match OD.option_type [`bool]
				send Output "no_argument"
			else
				send Output "required_argument"

			send Output
				", 0, 0 },

			# Save the index.
			OS.Ind = Ind
			Ind = Ind + 1
		}
	}

	send Output
		"	{ 0, 0, 0, 0 }
		"};
		"
}

void optValues( P: program )
{
	Ind: int = 0
	for OD: option_def in P {
		switch OD.option_type
		case [`bool] {
			send Output
				"bool [OD.id] = false;
		}
		case [`string] {
			send Output
				"std::string [OD.id];

		}
		case [`long] {
			send Output
				"long [OD.id];
		}
	}

	send Output "
}

void parseOption( OD: option_def Src: str )
{
	switch OD.option_type
	case [`bool] {
		send Output
			"[OD.id] = true;
	}
	case [`string] {
		send Output
			"[OD.id] = [Src];

	}
	case [`long] {
		send Output
			"char *eptr;
			"if ( *[Src] == 0 )
			"	log_FATAL( \"no characters found in [OD.id] (long) value\" );
			"
			"[OD.id] = strtol( [Src], &eptr, 10 );
			"
			"if ( *eptr != 0 )
			"	log_FATAL( \"invalid characters in [OD.id] (long) value: \" << [Src] );
			"if ( ( [OD.id] == LONG_MAX || [OD.id] == LONG_MIN) && errno == ERANGE )
			"	log_FATAL( \"[OD.id] (long) out of range: \" << [Src] );
			"if ( [OD.id] == 0 && errno != 0 )
			"	log_FATAL( \"failed to parse [OD.id] (long): \" << [Src] );
	}	
}

void parseOptions( P: ref<program> )
{
	send Output
		"void MainGen::parseOptions()
		"{
		"	while ( true ) {
		"		int option_index = 0;
		"		int c = getopt_long( argc, argv, optstring, longopts, &option_index );
		"		if ( c == -1 )
		"			break;
		"		switch ( c ) {

	for OD: option_def in P {
		for OS: option_spec in OD {
			require OS [`- short]

			send Output
				"			case '[OS.short]': {
				"				[parseOption( OD 'optarg' )]
				"				break;
				"			}
		}
	}
	
	send Output
		"			case 0: {
		"				switch ( option_index ) {

	for OD: option_def in P {
		for OS: option_spec in OD {
			require OS [`-- long]
			send Output
				"				case [OS.Ind]:
				"					[parseOption( OD '(argv[optind-1])' )]
				"					break;
		}
	}

	send Output
		"				}
		"				break;
		"			}
		"			case ':': 
		"			case '?': {
		"				const char *msg = ( c == '?' ) ? 
		"						\"unknown option: \" :
		"						\"missing argument for option: \";
		"
		"				if ( optopt == 0 )
		"					log_FATAL( msg << argv\[optind-1\] );
		"				else 
		"					log_FATAL( msg << \"-\" << (char)optopt );
		"				break;
		"			}
		"		}
		"	}
		"}
}

void generateMainCode( P: ref<program> )
{
	Output = new parser<out_c::out_c>
	Thread: thread = ThreadMap->find( 'Main' )

	send Output
		~/*
		~ * main_gen.cc
		~ */
		~

	send Output
		~#include <iostream>
		~#include <unistd.h>
		~#include <getopt.h>
		~#include <errno.h>
		~#include <stdlib.h>
		~#include <limits.h>
		~#include <string.h>
		~#include <signal.h>
		~
		~#include "main.h"

	threadIncludes( Thread )
	send Output ~
	
	optString( P )
	longOpts( P )
	optValues( P )
	parseOptions( P )

	send Output
		~void MainGen::maybeBackground()
		~{
		~	if ( background ) {
		~		int result = daemon( 0, 0 );
		~		if ( result == -1 )
		~			log_FATAL( "damonize: " << strerror(errno) );
		~	}
		~}

	createChildDefs( Thread )

	send Output
		~int MainGen::start()
		~{
		~	parseOptions();
		~	maybeBackground();
		~	sigmaskSetup();
		~	return main();
		~}
		~

	# Mask the signals we wish to funnel to the main thread. This will be the
	# signal set that all threads should receive.
	send Output
		~void MainGen::sigmaskSetup()
		~{
		~	sigset_t set;
		~
		~	sigemptyset( &set );
		~	sigaddset( &set, SIGHUP );
		~	sigaddset( &set, SIGINT );
		~	sigaddset( &set, SIGQUIT );
		~	sigaddset( &set, SIGKILL );
		~	sigaddset( &set, SIGTERM );
		~	sigaddset( &set, SIGCHLD );
		~
		~	pthread_sigmask( SIG_SETMASK, &set, 0 );
		~}
		~

	send Output
		~int MainGen::mainSignal()
		~{
		~	sigset_t set;
		~	sigfillset( &set );
		~
		~	sigemptyset( &set );
		~	sigaddset( &set, SIGHUP );
		~	sigaddset( &set, SIGINT );
		~	sigaddset( &set, SIGQUIT );
		~	sigaddset( &set, SIGKILL );
		~	sigaddset( &set, SIGTERM );
		~	sigaddset( &set, SIGUSR1 );
		~	sigaddset( &set, SIGUSR2 );
		~	sigaddset( &set, SIGCHLD );
		~	pthread_sigmask( SIG_SETMASK, &set, 0 );
		~
		~	bool cont = true;
		~	while ( cont ) {
		~		int sig;
		~		int r = sigwait( &set, &sig );
		~		if ( r != 0 ) {
		~			log_ERROR( "sigwait returned: " << strerror(r) );
		~			continue;
		~		}
		~
		~		switch ( sig ) {
		~		case SIGHUP:
		~		{
		~			log_message( "received sighup" );
		~			break;
		~		}
		~		case SIGINT:
		~		case SIGQUIT:
		~		case SIGKILL:
		~		case SIGTERM:
		~		{
		~			log_message( "breaking from signal loop" );
		~			cont = false;
		~			break;
		~		}
		~		default: {
		~			log_message( "received sig: " << sig );
		~			break;
		~		}}
		~	}
		~	return 0;
		~}
		~

	joinDef( Thread )
	sendsToCode( Thread )

	send Output
		~int main( int argc, char *const*argv )
		~{
		~	genf::lf = new lfdostream( 2 );
		~	MainThread main;
		~	main.init( argc, argv );
		~	return main.start();
		~}
		~

	send Output [] eos
	writeOutput( Output->tree 'main_gen.cc' )
}
