void generatePacketHeader( P: program )
{
	Output = new parser<out_c::out_c>()

	send Output
		~/*
		~ * packet_gen.h
		~ */
		~
		~#ifndef __PACKET_GEN_H
		~#define __PACKET_GEN_H
		~
		~#include <sys/types.h>
		~#include <sys/socket.h>
		~
		~struct PacketWriter;
		~

	ID: int = 1

	for Packet: packet in PacketMap {
		send Output
			"struct [Packet->Id]
			"{

		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`bool] {
				send Output
					"	bool [FD.id];
			}
			case [`long] {
				send Output
					"	long [FD.id];
			}
			case [`string] {
				send Output
					"	const char *[FD.id];
			}
		}

		send Output
			"	static [Packet->Id] *open( PacketWriter *writer );
			"	static void send( PacketWriter *writer );
			"	static const unsigned short ID = [ID];
			"};
			"

		ID = ID + 1
	}

	send Output
		~#endif
		eos

	writeOutput( Output->tree, 'packet_gen.h' )
}

void packetRecvDecls( Thread: thread )
{
	for Packet: packet in Thread->RecvsPktList {
		send Output
			"	virtual void recv[Packet->Id]( int fd, [Packet->Id] *pkt ) = 0;
	}
}

void packetReceives( Thread: thread )
{
	send Output
		"void [Thread->Id]Gen::data( SelectFd *fd )
		"{
		"	log_message( \"select in data call\" );
		"	PacketHeader packetHeader;
		"	int len = read( fd->fd, &packetHeader, sizeof(packetHeader) );
		"	log_message( \"header read: \" << len );
		"	if ( len == 0 ) {
		"		close( fd );
		"		return;
		"	}
		"
		"	switch ( packetHeader.msgId )  {

	for Packet: packet in Thread->RecvsPktList {
		send Output
			"		case (int) [Packet->Id]::ID: {
			"			[Packet->Id] pkt;
			"			len = read( fd->fd, &pkt, sizeof(pkt) );
			"			if ( len == 0 ) {
			"				close( fd );
			"				return;
			"			}
			"			recv[Packet->Id]( fd->fd, &pkt );
			"			break;
			"		}
	}

	send Output
		"	}
		"}
		"
		"void [Thread->Id]Gen::close( SelectFd *fd )
		"{
		"	log_message( \"closing: \" << fd->fd );
		"	::close( fd->fd );
		"	selectFdList.detach( fd );
		"	delete fd;
		"}   
		"
}


void generatePacketCode( P: program )
{
	Output = new parser<out_c::out_c>()

	send Output
		~/*
		~ * packet_gen.cc
		~ */
		~
		~#include "packet_gen.h"
		~#include <genf/thread.h>
		~

	for Packet: packet in PacketMap {
		send Output
			"[Packet->Id] *[Packet->Id]::open( PacketWriter *writer )
			"{
			"	writer->mlen = 0;
			"
			"	/* Place the header. */
			"	int offset = writer->allocBytes( sizeof(PacketHeader) );
			"	PacketHeader *header = (PacketHeader*) ( writer->data() + offset );
			"	
			"	header->msgId = [Packet->Id]::ID;
			"	header->writerId = 1; // writer->id;
			"
			"	/* Place the struct. */
			"	offset = writer->allocBytes( sizeof([Packet->Id]) );
			"	[Packet->Id] *msg = ([Packet->Id]*) ( writer->data() + offset );
			"
			"	header->length = writer->mlen;
			"	writer->toSend = header;
			"
			"	return msg;
			"}
			"

		send Output
			"void [Packet->Id]::send( PacketWriter *writer )
			"{
			"	log_message( \"sending \" << writer->length() << \" bytes\" );
			"	::write( writer->fd, writer->data(), writer->length() );
			"}
			"
	}

	send Output [] eos

	writeOutput( Output->tree, 'packet_gen.cc' )
}
