namespace packet_name
	lex
		literal `-
		token component /[A-Z][a-z]*/
	end

	def name
		[component*]
	
	str makeLower( Name: str )
	{
		Lower: str = ""
		parse PN: packet_name::name[Name]
		for C: packet_name::component in PN {
			if Lower.length > 0
				Lower = Lower + "-"
			Lower = Lower + tolower($C)
		}
		return Lower
	}

end

void generatePacketHeader( P: program )
{
	Output = new parser<out_c::out_c>()

	send Output
		~/*
		~ * packet_gen.h
		~ */
		~
		~#ifndef __PACKET_GEN_H
		~#define __PACKET_GEN_H
		~
		~#include <sys/types.h>
		~#include <sys/socket.h>
		~#include <errno.h>
		~
		~#include <genf/packet.h>
		~#include <genf/thread.h>
		~
		~struct PacketWriter;
		~struct PacketConnection;
		~struct ItWriter;
		~
		~namespace Overlay {

	#
	# Overlay structs.
	#
	for Struct: _struct in StructMap {
		send Output
			"struct [Struct->Id]
			"{

		for FD: struct_field_def in Struct->StructDef {
			switch FD.struct_field_type
			case [`bool] {
				send Output
					"	bool [FD.id];
			}
			case [`long] {
				send Output
					"	long [FD.id];
			}
			case [`string] {
				send Output
					"	char *[FD.id];
			}
		}

		send Output
			"	long next;
			"};
			"
	}

	for Packet: packet in PacketMap {
		send Output
			"struct [Packet->Id]
			"	: public PacketBase
			"{

		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`bool] {
				send Output
					"	bool [FD.id];
			}
			case [`long] {
				send Output
					"	long [FD.id];
			}
			case [`string] {
				send Output
					"	char *[FD.id];
			}
			case [list_type] {
				TypeId: id = FD.pkt_field_type.list_type.id
				send Output
					"	long head_[FD.id];

			}
		}

		send Output
			"};
			"
	}

	send Output
		~}
		~

	#
	# Packing structs
	#

	send Output
		~namespace Packer {

	for Struct: _struct in StructMap {
		send Output
			"struct [Struct->Id]
			"{

		for FD: struct_field_def in Struct->StructDef {
			switch FD.struct_field_type
			case [`bool] {
				send Output
					"	void set_[FD.id]( long _[FD.id] )
					"		{ overlay->[FD.id] = _[FD.id]; }
			}
			case [`long] {
				send Output
					"	void set_[FD.id]( long _[FD.id] )
					"		{ overlay->[FD.id] = _[FD.id]; }
			}
			case [`string] {
				send Output
					"	void set_[FD.id]( const char *d );
			}
		}

		send Output
			"	Overlay::[Struct->Id] *overlay;
			"	PacketWriter *writer;
			"};
			"
	}

	for Packet: packet in PacketMap {
		send Output
			"struct [Packet->Id]
			"	: public PacketBase
			"{
			"	[Packet->Id]( PacketConnection *pc  ) : writer( pc ) { open( &writer ); }
			"	[Packet->Id]( ItWriter *itw ) : writer( itw ) { open( &writer ); }
			"	PacketWriter writer;

		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`bool] {
				send Output
					"	void set_[FD.id]( long _[FD.id] )
					"		{ overlay->[FD.id] = _[FD.id]; }
			}
			case [`long] {
				send Output
					"	void set_[FD.id]( long _[FD.id] )
					"		{ overlay->[FD.id] = _[FD.id]; }
			}
			case [`string] {
				send Output
					"	void set_[FD.id]( const char *d );
			}
			case [list_type] {
				TypeId: id = FD.pkt_field_type.list_type.id
				send Output
					"	Overlay::[TypeId] *tail_[FD.id];
					"	void alloc_[FD.id]( [TypeId] &el );

			}
		}

		send Output
			"	void send()
			"	{
			"		PacketBase::send( &writer );
			"	}
			"	Overlay::[Packet->Id] *overlay;
			"	void open( PacketWriter *writer );
			"	static const unsigned short ID = [Packet->AppID * 1000 + Packet->NumID];
			"	static const char name\[\];
			"	static const char lower\[\];
			"};
			"
	}

	send Output
		~}
		~
	#
	# Reading structs structs
	#

	send Output
		~namespace Record {

	for Struct: _struct in StructMap {
		send Output
			"struct [Struct->Id]
			"{
			"	[Struct->Id]( Rope *rope, long offset );

		for FD: struct_field_def in Struct->StructDef {
			switch FD.struct_field_type
			case [`bool] {
				send Output
					"	bool [FD.id];
			}
			case [`long] {
				send Output
					"	long [FD.id];
			}
			case [`string] {
				send Output
					"	char *[FD.id];
			}
		}

		send Output
			"	void advance();
			"	bool valid();
			"	void load();
			"	long next;
			"	Rope *rope;
			"	long offset;
			"};
			"
	}

	for Packet: packet in PacketMap {
		send Output
			"struct [Packet->Id]
			"	: public PacketBase
			"{
			"	[Packet->Id]( Rope *rope, long offset );

		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`bool] {
				send Output
					"	bool [FD.id];
			}
			case [`long] {
				send Output
					"	long [FD.id];
			}
			case [`string] {
				send Output
					"	char *[FD.id];
			}
			case [list_type] {
				TypeId: id = FD.pkt_field_type.list_type.id
				send Output
					"	long head_[FD.id];

			}
		}

		send Output
			"	Rope *rope;
			"	static const unsigned short ID = [Packet->AppID * 1000 + Packet->NumID];
			"	static const char name\[\];
			"	static const char lower\[\];
			"};
			"
	}

	send Output
		~}
		~
		~const char *packetLowerName( unsigned short id );
		~
		~#endif
		eos

	writeOutput( Output->tree, 'packet_gen.h' )
}

void packetRecvDecls( Thread: thread )
{
	send Output
		"	virtual void dispatchPacket( SelectFd *fd, SelectFd::Recv &recv );

	for Packet: packet in Thread->RecvsPktList {
		send Output
			"	virtual void recv[Packet->Id]( SelectFd *fd, Record::[Packet->Id] *pkt ) = 0;
	}
}

void packetReceives( Thread: thread )
{
	send Output
		"void [Thread->Id]Gen::dispatchPacket( SelectFd *fd, SelectFd::Recv &recv )
		"{
		"		switch ( recv.head->msgId ) {

	for Packet: packet in Thread->RecvsPktList {
		send Output
			"			case (int) Record::[Packet->Id]::ID: {
			"				Record::[Packet->Id] rec( &recv.buf,
			"					sizeof(PacketBlockHeader) + sizeof(PacketHeader) );
			"				recv[Packet->Id]( fd, &rec );
			"				break;
			"			}
	}

	send Output
		"		}
		"}
		"
		"
}




void generatePacketCode( P: program )
{
	Output = new parser<out_c::out_c>()

	send Output
		~/*
		~ * packet_gen.cc
		~ */
		~
		~#include "itq_gen.h"
		~#include "packet_gen.h"
		~#include <genf/thread.h>
		~

	for Struct: _struct in StructMap {
		for FD: struct_field_def in Struct->StructDef {
			switch FD.struct_field_type
			case [`string] {
				send Output
					"void Packer::[Struct->Id]::set_[FD.id]( const char *src )
					"{
					"	int length = strlen( src ) + 1;
					"	long offset = 0;
					"	char *dest = writer->allocBytes( length, offset );
					"	overlay->[FD.id] = (char*)offset;
					"	memcpy( dest, src, length );
					"}
					"
			}
		}
	}

	for Packet: packet in PacketMap {
		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`string] {
				send Output
					"void Packer::[Packet->Id]::set_[FD.id]( const char *src )
					"{
					"	int length = strlen( src ) + 1;
					"	long offset = 0;
					"	char *dest = writer.allocBytes( length, offset );
					"	overlay->[FD.id] = (char*)offset;
					"	memcpy( dest, src, length );
					"}
					"
			}
			case [L: list_type] {
				TypeId: id = L.id
				send Output
					"void Packer::[Packet->Id]::alloc_[FD.id]( [TypeId] &el )
					"{
					"	long offset = 0;
					"	Overlay::[TypeId] *elov = (Overlay::[TypeId]*)writer.allocBytes( 
					"			sizeof(Overlay::[TypeId]), offset );
					"	memset( elov, 0, sizeof(Overlay::[TypeId]) );
					"	
					"	if ( overlay->head_[FD.id] == 0 )
					"		overlay->head_[FD.id] = offset;
					"	else
					"		tail_[FD.id]->next = offset;
					"
					"	tail_[FD.id] = elov;
					"	el.overlay = elov;
					"	el.writer = &writer;
					"}
					"
			}
		}

		send Output
			"void Packer::[Packet->Id]::open( PacketWriter *writer )
			"{
			"	this->overlay = (Overlay::[Packet->Id]*)
			"			PacketBase::open( writer, [Packet->Id]::ID,
			"			sizeof(Overlay::[Packet->Id]) );
			"}
			"

		Lower: str = packet_name::makeLower( Packet->Id )
		send Output
			"const char Packer::[Packet->Id]::name\[\] = \"[Packet->Id]\";
			"const char Packer::[Packet->Id]::lower\[\] = \"[Lower]\";
			"

	}

	for Struct: _struct in StructMap {
		send Output
			"Record::[Struct->Id]::[Struct->Id]( Rope *rope, long offset )
			"	: rope(rope), offset(offset) 
			"{
			"	if ( offset != 0 )
			"		load();
			"}
			"
			"void Record::[Struct->Id]::advance()
			"{
			"	offset = next;
			"	if ( offset != 0 )
			"		load();
			"}
			"
			"bool Record::[Struct->Id]::valid()
			"{
			"	return offset != 0;
			"}
			"
			"void Record::[Struct->Id]::load()
			"{
			"	Overlay::[Struct->Id] *overlay = 
			"		(Overlay::[Struct->Id]*)Thread::pktFind( rope, offset );;

		for FD: struct_field_def in Struct->StructDef {
			switch FD.struct_field_type
			case [`bool] {
				send Output
					"	this->[FD.id] = overlay->[FD.id];
			}
			case [`long] {
				send Output
					"	this->[FD.id] = overlay->[FD.id];
			}
			case [`string] {
				send Output
					"	this->[FD.id] = Thread::pktFind( rope, (long)overlay->[FD.id] );
			}
			send Output
				"	this->next = overlay->next;
		}

		send Output
			"}
	}

	for Packet: packet in PacketMap {
		send Output
			"Record::[Packet->Id]::[Packet->Id]( Rope *rope, long offset )
			"{
			"	this->rope = rope;
			"	Overlay::[Packet->Id] *overlay = 
			"		(Overlay::[Packet->Id]*)Thread::pktFind( rope, offset );;

			
		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`bool] {
				send Output
					"	this->[FD.id] = overlay->[FD.id];
			}
			case [`long] {
				send Output
					"	this->[FD.id] = overlay->[FD.id];
			}
			case [`string] {
				send Output
					"	this->[FD.id] = Thread::pktFind( rope, (long)overlay->[FD.id] );
			}
			case [L: list_type] {
				TypeId: id = L.id
				send Output
					"	this->head_[FD.id] = overlay->head_[FD.id];

			}
		}

		send Output
			"}

		Lower: str = packet_name::makeLower( Packet->Id )
		send Output
			"const char Record::[Packet->Id]::name\[\] = \"[Packet->Id]\";
			"const char Record::[Packet->Id]::lower\[\] = \"[Lower]\";
			"

	}

	send Output
		"const char *packetLowerName( unsigned short id )
		"{
		"	switch ( id ) {

	for Packet: packet in PacketMap {
		send Output
		"		case Record::[Packet->Id]::ID: return Record::[Packet->Id]::lower;
	}

	send Output
		"	}
		"	return \"unknown\";
		"}

	send Output [] eos

	writeOutput( Output->tree, 'packet_gen.cc' )
}
