void generatePacketHeader( P: program )
{
	Output = new parser<out_c::out_c>()

	send Output
		~/*
		~ * packet_gen.h
		~ */
		~
		~#ifndef __PACKET_GEN_H
		~#define __PACKET_GEN_H
		~
		~#include <sys/types.h>
		~#include <sys/socket.h>
		~
		~struct PacketWriter;
		~

	ID: int = 1

	for Packet: packet in PacketMap {
		send Output
			"struct [Packet->Id]
			"{

		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`bool] {
				send Output
					"	bool [FD.id];
			}
			case [`long] {
				send Output
					"	long [FD.id];
			}
			case [`string] {
				send Output
					"	char *[FD.id];
					"	void set_[FD.id]( PacketWriter *writer, const char *d );
			}
		}

		send Output
			"	static [Packet->Id] *open( PacketWriter *writer );
			"	static void send( PacketWriter *writer );
			"	static const unsigned short ID = [ID];
			"};
			"

		ID = ID + 1
	}

	send Output
		~#endif
		eos

	writeOutput( Output->tree, 'packet_gen.h' )
}

void packetRecvDecls( Thread: thread )
{
	for Packet: packet in Thread->RecvsPktList {
		send Output
			"	virtual void recv[Packet->Id]( SelectFd *fd, [Packet->Id] *pkt ) = 0;
	}
}

void packetReceives( Thread: thread )
{
	send Output
		"void [Thread->Id]Gen::data( SelectFd *fd )
		"{
		"	PacketHeader head;
		"
		"	int len = ::read( fd->fd, &head, sizeof(head) );
		"	if ( len == 0 ) {
		"		close( fd );
		"		return;
		"	}
		"
		"	int *nextLen = &head.firstLen;
		"
		"	while ( true ) {
		"		char *data = fd->recvBuf.appendBlock( sizeof(PacketBlockHeader) + *nextLen );
		"
		"		int need = sizeof(int) + *nextLen;
		"		int have = 0;
		"		
		"		while ( have < need ) {
		"			int len = ::read( fd->fd, data + have, need - have );
		"			if ( len > 0 ) {
		"				have += len;
		"			}
		"		}
		"
		"		nextLen = (int*)data;
		"		if ( *nextLen == 0 )
		"			break;
		"	}
		"
		"	switch ( head.msgId )  {

	for Packet: packet in Thread->RecvsPktList {
		send Output
			"		case (int) [Packet->Id]::ID: {
			"			[Packet->Id] *pkt = ([Packet->Id]*) pktFind( &fd->recvBuf, 0 );
		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`string] {
				send Output
					"	pkt->[FD.id] = pktFind( &fd->recvBuf, (long)pkt->[FD.id] );
			}
		}
		send Output
			"			recv[Packet->Id]( fd, pkt );
			"			break;
			"		}
	}

	send Output
		"	}
		"	fd->recvBuf.empty();
		"}
		"
		"void [Thread->Id]Gen::close( SelectFd *fd )
		"{
		"	::close( fd->fd );
		"	selectFdList.detach( fd );
		"	delete fd;
		"}   
		"
}


void generatePacketCode( P: program )
{
	Output = new parser<out_c::out_c>()

	send Output
		~/*
		~ * packet_gen.cc
		~ */
		~
		~#include "packet_gen.h"
		~#include <genf/thread.h>
		~

	for Packet: packet in PacketMap {
		for FD: pkt_field_def in Packet->PacketDef {
			switch FD.pkt_field_type
			case [`string] {
				send Output
					"void [Packet->Id]::set_[FD.id]( PacketWriter *writer, const char *src )
					"{
					"	int length = strlen( src ) + 1;
					# We don't use the auto-allocated space in the first packet
					# block.
					"	long offset = writer->length() - sizeof(PacketHeader) - sizeof(PacketBlockHeader);
					"	char *dest = writer->allocBytes( length );
					"	(([Packet->Id]*)writer->content)->[FD.id] = (char*)offset;
					"	memcpy( dest, src, length );
					"}
					"
			}
		}

		send Output
			"[Packet->Id] *[Packet->Id]::open( PacketWriter *writer )
			"{
			"	writer->reset();
			"
			"	/* Place the header. */
			"	PacketHeader *header = (PacketHeader*) writer->allocBytes(
			"			sizeof(PacketHeader) + sizeof(PacketBlockHeader) );
			"	
			"	header->msgId = [Packet->Id]::ID;
			"	header->writerId = 1; // writer->id;
			"
			"	/* Place the struct. */
			"	[Packet->Id] *msg = ([Packet->Id]*) writer->allocBytes( sizeof([Packet->Id]) );
			"
			"	header->length = 0;
			"	writer->toSend = header;
			"	writer->content = msg;
			"
			"	return msg;
			"}
			"

		#
		# format:
		#   PacketHeader, which contains first len
		#   ( nextLen, block )*
		#
		# The block lengths don't include the size of the nextLen field (int)
		#
		# Probably change to
		#
		# ( nextlen block )* 
		#
		# Where packet header is included in the first block.
		#

		send Output
			"void [Packet->Id]::send( PacketWriter *writer )
			"{
			"	RopeBlock *rb = writer->buf.hblk;
			"
			"	writer->toSend->length = writer->length();
			"	writer->toSend->firstLen = writer->buf.length(rb) - sizeof(PacketHeader) - sizeof(PacketBlockHeader);
			"
			"	int n = 0;
			"	for ( ; rb != 0; rb = rb->next ) {
			"		char *data = writer->buf.data(rb);
			"		int blockLen = writer->buf.length(rb);
			"
			"		int *nextLen;
			"		if ( n == 0 ) {
			"			//data += sizeof(PacketHeader);
			"			//blockLen -= sizeof(PacketHeader);
			"			nextLen = (int*)(data + sizeof(PacketHeader));
			"		}
			"		else {
			"			data -= sizeof(int);
			"			blockLen += sizeof(int);
			"			nextLen = (int*)(data);
			"		}

			"		*nextLen = rb->next != 0 ? writer->buf.length(rb->next) : 0;
			"
			"		::write( writer->fd, data, blockLen );
			"
			"		n += 1;
			"	}
			"}
			"
	}

	send Output [] eos

	writeOutput( Output->tree, 'packet_gen.cc' )
}
