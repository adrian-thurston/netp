
void generateItqHeader( P: program )
{
	Output = cons parser<out_c::out_c>[]

	send Output
		~/*
		~ * itq_gen.h
		~ */
		~
		~#ifndef __ITQ_GEN_H
		~#define __ITQ_GEN_H
		~
		~struct ItHeader;
		~struct ItWriter;
		~struct ItQueue;
		~

	ID: int = 1

	for Message: message in deref MessageMap {
		send Output
			"struct [Message->Id]
			"{

		for FD: field_def in Message->MessageDef {
			switch FD.field_type
			case [`bool] {
				send Output
					"	bool [FD.id];
			}
			case [`long] {
				send Output
					"	long [FD.id];
			}
			case [`string] {
				send Output
					"	const char *[FD.id];
			}
		}

		send Output
			"	static [Message->Id] *open( ItWriter *writer );
			"	static [Message->Id] *read( ItQueue *queue, ItHeader *header );
			"	static const unsigned short ID = [ID];
			"};
			"

		ID = ID + 1
	}

	send Output
		~#endif
		eos

	writeOutput( Output.tree 'itq_gen.h' )
}

void generateItqCode( P: program )
{
	Output = cons parser<out_c::out_c>[]

	send Output
		~/*
		~ * itq_gen.cc
		~ */
		~
		~#include "itq_gen.h"
		~#include "thread.h"
		~

	for MD: message_def in P {
		send Output
			"[MD.id] *[MD.id]::open( ItWriter *writer )
			"{
			"	writer->mlen = 0;
			"
			"	/* Place the header. */
			"	ItHeader *header = (ItHeader*) writer->queue->allocBytes(
			"			writer, sizeof(ItHeader) );
			"	
			"	header->msgId = [MD.id]::ID;
			"	header->writerId = writer->id;
			"
			"	/* Place the struct. */
			"	[MD.id] *msg = ([MD.id]*) writer->queue->allocBytes(
			"			writer, sizeof([MD.id]) );
			"
			"	header->length = writer->mlen;
			"	writer->toSend = header;
			"
			"	return msg;
			"}
			"

		send Output
			"[MD.id] *[MD.id]::read( ItQueue *queue, ItHeader *header )
			"{
			"	ItWriter *writer = queue->writerVect\[header->writerId\];
			"	ItBlock *head = writer->head;
			"	int offset = writer->hoff;
			"
			"	if ( offset + sizeof(ItHeader) > head->size ) {
			"		head = head->next;
			"		offset = 0;
			"	}
			"	offset += sizeof( ItHeader );
			"
			"	if ( offset + sizeof([MD.id]) > head->size ) {
			"		head = head->next;
			"		offset = 0;
			"	}
			"	[MD.id] *msg = ([MD.id]*)(head->data + offset);
			"	offset += sizeof( [MD.id] );
			"
			"	return msg;
			"}
			"
	}

	send Output [] eos

	writeOutput( Output.tree 'itq_gen.cc' )
}
